subprojects {
    afterEvaluate { project ->
        if (!plugins.hasPlugin('maven-publish')) {
            return
        }
        // Fix project.group
        def fixedGroup
        if (project.parent == rootProject) {
            fixedGroup = "${rootProject.group}.${project.name}"
        } else {
            fixedGroup = "${rootProject.group}.${project.parent.name}"
        }
        def fixedVersion
        if (plugins.hasPlugin('com.android.library')) {
            fixedVersion = android.defaultConfig.versionName
        } else if (plugins.hasPlugin('java-library')) {
            fixedVersion = POM_VERSION
        } else {
            println("Ignore publish ${project.displayName}")
            return
        }

        // Publish Module: project ':protect:stringFucker-gradle-plugin' -> fan.akua.akuax.protect:stringFucker-gradle-plugin:0.0.4
        println("Publish Module: ${project.displayName} -> ${fixedGroup}:${project.name}:${fixedVersion}")
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    group fixedGroup
                    artifactId project.name
                    version fixedVersion
                    if (plugins.hasPlugin('com.android.library')) {
                        afterEvaluate {
                            from components.release
                        }
                    } else if (plugins.hasPlugin('java-library')) {
                        from components.java
                    }

                    pom {
                        name = POM_NAME
                        description = POM_DESCRIPTION
                        url = 'https://github.com/AquaApps/AkuaX'
                        licenses {
                            license {
                                name = 'MIT License'
                                url = 'https://github.com/AquaApps/AkuaX/blob/main/LICENSE'
                            }
                        }
                        developers {
                            developer {
                                name = 'Akua / Aqua'
                                url = 'https://github.com/A-kua'
                            }
                        }
                    }
                }
            }
            repositories {
                maven {
                    allowInsecureProtocol true
                    url 'http://maven.akua.fan:8081/repository/akuax/'
                    credentials {
                        username "xxx"
                        password "xxx"
                    }
                }
            }
        }
    }

    afterEvaluate {
        if (!plugins.hasPlugin('maven-publish') || !plugins.hasPlugin('signing')) {
            return
        }
        if (plugins.hasPlugin('com.android.library')) {
            android {
                publishing {
                    singleVariant("release") {
                        withSourcesJar()
                        withJavadocJar()
                    }
                }
            }
        } else if (plugins.hasPlugin('java-library')) {
            java {
                withJavadocJar()
                withSourcesJar()
            }
        } else {
            println("Ignore singing ${project.displayName}")
            return
        }

        signing {
            useGpgCmd()
            sign publishing.publications.mavenJava
        }
    }
}
