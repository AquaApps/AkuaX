subprojects {
    afterEvaluate { project ->
        if (!plugins.hasPlugin('maven-publish')) {
            return
        }
        if (project.parent == rootProject) {
            group = "${rootProject.group}.${project.name}"
        } else {
            group = "${rootProject.group}.${project.parent.name}"
        }
        if (plugins.hasPlugin('com.android.library')) {
            version = android.defaultConfig.versionName
        } else if (plugins.hasPlugin('java-library')) {
            version = POM_VERSION
        } else {
            println("Ignore publish ${project.displayName}")
            return
        }

        println("Process Module: ${project.displayName}: ${group}:${project.name}:${version}")
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    group project.group
                    artifactId project.name
                    version version

                    afterEvaluate {
                        if (plugins.hasPlugin('com.android.library')) {
                            from components.release
                        } else if (plugins.hasPlugin('java-library')) {
                            from components.java
                        }
                    }

                    pom {
                        name = POM_NAME
                        description = POM_DESCRIPTION
                        url = 'https://github.com/AquaApps/AkuaX'
                        licenses {
                            license {
                                name = 'MIT License'
                                url = 'https://github.com/AquaApps/AkuaX/blob/main/LICENSE'
                            }
                        }
                        developers {
                            developer {
                                name = 'Akua / Aqua'
                                url = 'https://github.com/A-kua'
                            }
                        }
                    }
                }
            }
            repositories {
                maven {
                    allowInsecureProtocol true
                    url 'http://maven.akua.fan:8081/repository/akuax/'
                    credentials {
                        username "xxx"
                        password "xxx"
                    }
                }
            }
        }
    }
    afterEvaluate {
        if (!plugins.hasPlugin('maven-publish') || !plugins.hasPlugin('signing')) {
            return
        }
        if (plugins.hasPlugin('com.android.library')) {
            android {
                publishing {
                    singleVariant("release") {
                        withSourcesJar()
                        withJavadocJar()
                    }
                }
            }
        } else if (plugins.hasPlugin('java-library')) {
            java {
                withJavadocJar()
                withSourcesJar()
            }
        } else {
            println("Ignore singing ${project.displayName}")
            return
        }


        signing {
            useGpgCmd()
            sign publishing.publications
        }
    }
}
